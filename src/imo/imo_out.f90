!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++  
!
!  Module : i m o _ o u t
!
!  Purpose : diagnostics and output of IMO model
!
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
!
! Copyright (C) 2017-2024 Potsdam Institute for Climate Impact Research,
!                         Matteo Willeit
!
! This file is part of CLIMBER-X.
!
! CLIMBER-X is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
! CLIMBER-X is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
! You should have received a copy of the GNU General Public License
! along with CLIMBER-X.  If not, see <http://www.gnu.org/licenses/>.
!
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
module imo_out

  use precision, only : wp
  use dim_name, only: dim_x, dim_y, dim_time, dim_month
  use constants, only : rho_i
  use timer, only : time_soy_imo, time_eoy_imo, time_eom_imo, time_out_imo
  use timer, only : year_clim, year_now, mon, time_out_ts, ny_out_ts, y_out_ts_clim, n_accel, nmon_year, nstep_mon_imo, nstep_year_imo
  use control, only : out_dir
  use imo_def, only : imo_class, ts_out, s_out 
  use imo_params, only : l_monthly_output
  use ncio
  use coord, only : grid_class

  implicit none

  private
  public :: imo_diag, imo_diag_init


contains

  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  ! Function :  i m o _ d i a g _ i n i t
  ! Purpose  :  Initialize netcdf output for imo
  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  subroutine imo_diag_init(imo)

    implicit none

    type(imo_class) :: imo

    integer :: k
    character (len=256) :: fnm

    imo%nout = 0

    ! allocate
    allocate(imo%ann_ts(ny_out_ts))

    ! initialize netcdf output
    fnm = trim(out_dir)//"/imo_"//trim(imo%grid%name)//"_ts.nc"
    call ts_nc(fnm)

    fnm = trim(out_dir)//"/imo_"//trim(imo%grid%name)//".nc"
    call imo_nc(fnm,imo%grid)

    allocate(imo%ann_s%mask_ocn_lake   (imo%grid%G%nx,imo%grid%G%ny))
    allocate(imo%ann_s%mask_ice_shelf   (imo%grid%G%nx,imo%grid%G%ny))
    allocate(imo%ann_s%zb         (imo%grid%G%nx,imo%grid%G%ny))
    allocate(imo%ann_s%t_imo    (imo%grid%G%nx,imo%grid%G%ny))
    allocate(imo%ann_s%s_imo    (imo%grid%G%nx,imo%grid%G%ny))
    allocate(imo%ann_s%t_disc    (imo%grid%G%nx,imo%grid%G%ny))
    allocate(imo%ann_s%s_disc    (imo%grid%G%nx,imo%grid%G%ny))
    allocate(imo%ann_s%t_freeze   (imo%grid%G%nx,imo%grid%G%ny))
    allocate(imo%ann_s%imo        (imo%grid%G%nx,imo%grid%G%ny))
    allocate(imo%ann_s%imo_ann    (imo%grid%G%nx,imo%grid%G%ny))
    allocate(imo%ann_s%imo_ann_mask(imo%grid%G%nx,imo%grid%G%ny))

    do k=1,nmon_year
      allocate(imo%mon_s(k)%mask_ice_shelf   (imo%grid%G%nx,imo%grid%G%ny))
      allocate(imo%mon_s(k)%zb         (imo%grid%G%nx,imo%grid%G%ny))
      allocate(imo%mon_s(k)%t_imo      (imo%grid%G%nx,imo%grid%G%ny))
      allocate(imo%mon_s(k)%s_imo      (imo%grid%G%nx,imo%grid%G%ny))
      allocate(imo%mon_s(k)%t_freeze   (imo%grid%G%nx,imo%grid%G%ny))
      allocate(imo%mon_s(k)%imo        (imo%grid%G%nx,imo%grid%G%ny))
    enddo


   return

  end subroutine imo_diag_init


  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  !   Subroutine :  i m o _ d i a g
  !   Purpose    :  sea ice diagnostics
  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  subroutine imo_diag(imo)

    implicit none

    type(imo_class) :: imo

    integer :: m, y
    integer :: ppos
    character (len=256) :: fnm
    character (len=256) :: dom
    real(wp) :: mon_avg, ann_avg


    ! current index
    y = y_out_ts_clim

    mon_avg = 1._wp/nstep_mon_imo
    ann_avg = 1._wp/nstep_year_imo

    if( time_eoy_imo ) then

     ! integrated basal melt of ice shelf
     imo%ann_ts(y)%imo = sum(imo%imo_ann*imo%grid%area,mask=imo%mask_ice_shelf==1) * 1.e-6_wp ! kg/m2/a * km2 * m2/km2 * Gt/kg = Gt/a

     ! write to netcdf file 
     if (time_out_ts) then
       fnm = trim(out_dir)//"/imo_"//trim(imo%grid%name)//"_ts.nc"
       call ts_nc_write(fnm,imo%ann_ts(1:y),year_clim-y+1,y)
     endif
     ppos = scan(trim(imo%grid%name),"-")-1
     dom = trim(imo%grid%name(1:ppos)) 
     ! print header
     if (mod(year_clim,10).eq.1) then
       print '(a7,a9,a7)','imo_'//dom,'year','imo'
     endif

     ! print values
     print '(a7,i9,1F7.0)', &
       'imo_'//dom,year_now,imo%ann_ts(y)%imo

    endif


    ! spatially explicit output
    if ( time_out_imo ) then

      if( time_soy_imo ) then
        do m=1,nmon_year
          imo%mon_s(m)%mask_ice_shelf   = 0._wp 
          imo%mon_s(m)%zb         = 0._wp 
          imo%mon_s(m)%t_imo      = 0._wp 
          imo%mon_s(m)%s_imo      = 0._wp 
          imo%mon_s(m)%t_freeze   = 0._wp 
          imo%mon_s(m)%imo        = 0._wp 
        enddo
      endif

      imo%mon_s(mon)%mask_ice_shelf   = imo%mon_s(mon)%mask_ice_shelf   + imo%mask_ice_shelf   * mon_avg
      imo%mon_s(mon)%zb         = imo%mon_s(mon)%zb         + imo%zb         * mon_avg
      imo%mon_s(mon)%t_imo      = imo%mon_s(mon)%t_imo      + imo%t_imo      * mon_avg
      imo%mon_s(mon)%s_imo      = imo%mon_s(mon)%s_imo      + imo%s_imo      * mon_avg
      imo%mon_s(mon)%t_freeze   = imo%mon_s(mon)%t_freeze   + imo%t_freeze   * mon_avg
      imo%mon_s(mon)%imo        = imo%mon_s(mon)%imo        + imo%imo        * mon_avg

    endif

    if (time_out_imo .and. time_eoy_imo) then

      imo%ann_s%imo_ann = imo%imo_ann / rho_i   ! m(ice)/a

      imo%ann_s%t_disc = imo%t_disc
      imo%ann_s%s_disc = imo%s_disc

      imo%nout = imo%nout+1
      call imo_diag_out(imo,imo%grid)
    endif


   return

  end subroutine imo_diag


  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  ! Function :  i m o _ d i a g _ o u t
  ! Purpose  :  write sea ice netcdf output
  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  subroutine imo_diag_out(imo,grid)

    implicit none

    type(imo_class) :: imo
    type(grid_class) :: grid
    integer :: k, ncid
    character (len=256) :: fnm


    ! Get annual values
    call imo_ave( imo%mon_s,imo%ann_s )
    
    imo%ann_s%mask_ocn_lake = imo%mask_ocn_lake
    imo%ann_s%imo_ann_mask = imo%ann_s%imo_ann * imo%ann_s%mask_ice_shelf  ! m(ice)/a

    ! write to file
    fnm = trim(out_dir)//"/imo_"//trim(imo%grid%name)//".nc"
    call nc_open(fnm,ncid)
    call nc_write(fnm,dim_time,dble(year_now), dim1=dim_time, start=[imo%nout], count=[1],ncid=ncid)    
    do k = 1, nmon_year
       call imo_nc_write(fnm,ncid,imo%mon_s(k),grid,k,imo%nout)
    end do
    call imo_nc_write(fnm,ncid,imo%ann_s,grid,nmon_year+1,imo%nout)
    call nc_close(ncid)


   return

  end subroutine imo_diag_out
  

  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  ! Function :  t s _ n c
  ! Purpose  :  initialize netcdf file for time series output
  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  subroutine ts_nc(fnm)

    implicit none

    character (len=*) :: fnm
    real(wp) :: empty_time(0)

    ! Create the netcdf file and the dimension variables
    call nc_create(fnm)
    call nc_write_dim(fnm,dim_time, x=empty_time, axis="t", units="years BP", unlimited=.TRUE.)

    return

  end subroutine ts_nc
 

  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  ! Function :  t s _ n c _ w r i t e
  ! Purpose  :  write time series to netcdf
  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  subroutine ts_nc_write(fnm,vars,nout,y)

    implicit none

    type(ts_out) :: vars(:)

    character (len=*) :: fnm
    integer :: nout, ncid, y, i

    call nc_open(fnm,ncid)
    call nc_write(fnm,"time",  real([(i,i=(year_now-(y-1)*n_accel),(year_now),(n_accel))],wp), dim1=dim_time,start=[nout],count=[y],ncid=ncid)    
    call nc_write(fnm,"imo", vars%imo, dims=[dim_time],start=[nout],count=[y],&
    long_name="integrated floating ice basal melt of ice sheets",units="Gt/a",ncid=ncid)
    call nc_close(ncid)


   return

  end subroutine ts_nc_write


  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  ! Function :  i m o _ n c
  ! Purpose  :  Initialize imo netcdf output
  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  subroutine imo_nc(fnm,grid)

    implicit none

    character (len=*) :: fnm
    type(grid_class) :: grid
    integer :: ncid
    real(wp) :: empty_time(0)


    ! Create the netcdf file and the dimension variables
    call nc_create(fnm)
    call nc_open(fnm,ncid)
    call nc_write_dim(fnm,dim_time, x=empty_time, axis="t", units="years BP", &
    unlimited=.TRUE.,ncid=ncid)
    call nc_write_dim(fnm, dim_month, x=1._wp, dx=1._wp, nx=13, axis="e", &
    units="months", ncid=ncid)
    call nc_write_dim(fnm, dim_y, x=grid%G%y0, dx=grid%G%dy, nx=grid%G%ny,&
    axis="y", units="km", ncid=ncid)
    call nc_write_dim(fnm, dim_x, x=grid%G%x0, dx=grid%G%dx, nx=grid%G%nx,&
    axis="x", units="km", ncid=ncid)

    call nc_write(fnm,"phi",sngl(grid%lat), dims=[dim_x,dim_y],start=[1,1],count=[grid%G%nx,grid%G%ny],ncid=ncid)
    call nc_write(fnm,"lambda",sngl(grid%lon), dims=[dim_x,dim_y],start=[1,1],count=[grid%G%nx,grid%G%ny],ncid=ncid)
    call nc_close(ncid)

   return

  end subroutine imo_nc


  ! +++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  ! Function :  i m o _ n c _ w r i t e
  ! Purpose  :  Output of imo netcdf
  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  subroutine imo_nc_write(fnm,ncid,vars,grid,ndat,nout)

    implicit none

    type(s_out) :: vars
    type(grid_class) :: grid

    character (len=*) :: fnm
    integer :: ndat, nout, ncid


    if (l_monthly_output) then
      call nc_write(fnm,"t_imo", sngl(vars%t_imo   ), dims=[dim_x,dim_y,dim_month,dim_time],start=[1,1,ndat,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="water temperature used for basal melt",grid_mapping="polar_stereographic",units="degC",ncid=ncid) 
      call nc_write(fnm,"s_imo", sngl(vars%s_imo   ), dims=[dim_x,dim_y,dim_month,dim_time],start=[1,1,ndat,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="water salinity used for basal melt",grid_mapping="polar_stereographic",units="psu",ncid=ncid) 
      call nc_write(fnm,"t_freeze", sngl(vars%t_freeze   ), dims=[dim_x,dim_y,dim_month,dim_time],start=[1,1,ndat,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="freezing temperature at the ice shelf base",grid_mapping="polar_stereographic",units="degC",ncid=ncid) 
      call nc_write(fnm,"imo", sngl(vars%imo   ), dims=[dim_x,dim_y,dim_month,dim_time],start=[1,1,ndat,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="basal mass melt of floating ice",grid_mapping="polar_stereographic",units="kg/m2/s",ncid=ncid) 
      call nc_write(fnm,"t_imo_mask", sngl(vars%t_imo*vars%mask_ice_shelf), dims=[dim_x,dim_y,dim_month,dim_time],start=[1,1,ndat,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="water temperature used for basal melt, masked with ice shelf mask",grid_mapping="polar_stereographic",units="degC",ncid=ncid) 
      call nc_write(fnm,"s_imo_mask", sngl(vars%s_imo*vars%mask_ice_shelf   ), dims=[dim_x,dim_y,dim_month,dim_time],start=[1,1,ndat,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="water salinity used for basal melt, masked with ice shelf mask",grid_mapping="polar_stereographic",units="psu",ncid=ncid) 
      call nc_write(fnm,"t_freeze_mask", sngl(vars%t_freeze*vars%mask_ice_shelf   ), dims=[dim_x,dim_y,dim_month,dim_time],start=[1,1,ndat,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="freezing temperature at the ice shelf base, masked with ice shelf mask",grid_mapping="polar_stereographic",units="degC",ncid=ncid) 
      call nc_write(fnm,"imo_mask", sngl(vars%imo*vars%mask_ice_shelf   ), dims=[dim_x,dim_y,dim_month,dim_time],start=[1,1,ndat,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="basal mass melt of floating ice, masked with ice shelf mask",grid_mapping="polar_stereographic",units="kg/m2/s",ncid=ncid) 
    else
    if (ndat.eq.13) then
      call nc_write(fnm,"t_imo", sngl(vars%t_imo   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="water temperature used for basal melt",grid_mapping="polar_stereographic",units="degC",ncid=ncid) 
      call nc_write(fnm,"s_imo", sngl(vars%s_imo   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="water salinity used for basal melt",grid_mapping="polar_stereographic",units="psu",ncid=ncid) 
      call nc_write(fnm,"t_freeze", sngl(vars%t_freeze   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="freezing temperature at the ice shelf base",grid_mapping="polar_stereographic",units="degC",ncid=ncid) 
      call nc_write(fnm,"imo", sngl(vars%imo   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="basal mass melt of floating ice",grid_mapping="polar_stereographic",units="kg/m2/s",ncid=ncid) 
      call nc_write(fnm,"t_imo_mask", sngl(vars%t_imo*vars%mask_ice_shelf   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="water temperature used for basal melt, masked with ice shelf mask",grid_mapping="polar_stereographic",units="degC",ncid=ncid) 
      call nc_write(fnm,"s_imo_mask", sngl(vars%s_imo*vars%mask_ice_shelf   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="water salinity used for basal melt, masked with ice shelf mask",grid_mapping="polar_stereographic",units="psu",ncid=ncid) 
      call nc_write(fnm,"t_freeze_mask", sngl(vars%t_freeze*vars%mask_ice_shelf   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="freezing temperature at the ice shelf base, masked with ice shelf mask",grid_mapping="polar_stereographic",units="degC",ncid=ncid) 
      call nc_write(fnm,"imo_mask", sngl(vars%imo*vars%mask_ice_shelf   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="basal mass melt of floating ice, masked with ice shelf mask",grid_mapping="polar_stereographic",units="kg/m2/s",ncid=ncid) 
    endif
    endif
    if (ndat.eq.13) then
      call nc_write(fnm,"t_disc", sngl(vars%t_disc   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="water temperature used for small-scale basal melt in ice sheet_model",grid_mapping="polar_stereographic",units="degC",ncid=ncid) 
      call nc_write(fnm,"s_disc", sngl(vars%s_disc   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="water salinity used for small-scale basal melt in ice sheet model",grid_mapping="polar_stereographic",units="psu",ncid=ncid) 
      call nc_write(fnm,"mask_ocn_lake", vars%mask_ocn_lake, dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="ocean/lake mask",grid_mapping="polar_stereographic",units="",ncid=ncid) 
      call nc_write(fnm,"mask_ice_shelf", sngl(vars%mask_ice_shelf   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="ice mask",grid_mapping="polar_stereographic",units="",ncid=ncid) 
      call nc_write(fnm,"zb", sngl(vars%zb   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="elevation of ice base relative to sea level",grid_mapping="polar_stereographic",units="m",ncid=ncid) 
      call nc_write(fnm,"zb_mask", sngl(vars%zb*vars%mask_ice_shelf   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1,1], &
        long_name="elevation of ice base relative to sea level, masked with ice shelf mask",grid_mapping="polar_stereographic",units="m",ncid=ncid) 
      call nc_write(fnm,"imo_ann", sngl(vars%imo_ann   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1], &
        long_name="annual basal melt of floating ice",grid_mapping="polar_stereographic",units="m/a",ncid=ncid) 
      call nc_write(fnm,"imo_ann_mask", sngl(vars%imo_ann_mask   ), dims=[dim_x,dim_y,dim_time],start=[1,1,nout],count=[grid%G%nx,grid%G%ny,1], &
        long_name="annual basal melt of floating ice, masked with ice shelf mask",grid_mapping="polar_stereographic",units="m/a",ncid=ncid) 
    endif

   return

  end subroutine imo_nc_write



  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  ! Function :  i m o _ a v e
  ! Purpose  :  Average (or sum) the sea ice fields
  ! ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  subroutine imo_ave(d,ave)

    implicit none

    type(s_out) :: d(:), ave

    integer :: k, n
    real(wp) :: div

    n = size(d)
    div = dble(n)

    ! Set all values to zero
    ave%mask_ice_shelf = 0._wp 
    ave%zb       = 0._wp 
    ave%t_imo    = 0._wp 
    ave%s_imo    = 0._wp 
    ave%t_freeze = 0._wp 
    ave%imo      = 0._wp 

    ! Loop over the time indices to sum up and average (if necessary)
    do k = 1, n
      ave%mask_ice_shelf  = ave%mask_ice_shelf  + d(k)%mask_ice_shelf  / div       
      ave%zb        = ave%zb        + d(k)%zb        / div       
      ave%t_imo     = ave%t_imo     + d(k)%t_imo     / div       
      ave%s_imo     = ave%s_imo     + d(k)%s_imo     / div       
      ave%t_freeze  = ave%t_freeze  + d(k)%t_freeze  / div       
      ave%imo       = ave%imo       + d(k)%imo       / div       
    end do

   return

  end subroutine imo_ave


end module imo_out
